apply plugin: 'com.android.application'
apply from: '../config/quality/quality.gradle'
apply from: '../config/jacoco.gradle'
apply plugin: 'realm-android'


android {
    compileSdkVersion 27
    buildToolsVersion '27.0.0'

    dexOptions {
        maxProcessCount 4
        preDexLibraries false
        javaMaxHeapSize "8g"
    }

    defaultConfig {
        defaultPublishConfig 'debug'
        applicationId "com.thoughtworks.whatyourward"
        minSdkVersion 19
        targetSdkVersion 27
        testInstrumentationRunner "com.thoughtworks.whatyourward.runner.RxAndroidJUnitRunner"
        versionCode 1000
        versionName '0.1.0'

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        def commonTestDir = 'src/commonTest/java'
        test {
            java.srcDir commonTestDir
        }
        androidTest {
            java.srcDir commonTestDir
        }
    }

    lintOptions {
        textOutput "stdout"
        textReport true
        checkAllWarnings true
        warningsAsErrors true
        showAll true
        explainIssues true
        abortOnError true
        ignoreWarnings false
        checkReleaseBuilds true
        htmlReport true
        xmlReport true
        lintConfig file("$projectDir/lint.xml")
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
        exclude 'META-INF/license/LICENSE.base64.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        animationsDisabled = true
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
            all {
                jacoco {
                    includeNoLocationClasses true
                }
                systemProperty 'robolectric.logging.enabled', 'true'
                systemProperty 'robolectric.logging', 'stdout'
                // Print test details in the terminal (or CI).
                testLogging {
                    events 'passed', 'skipped', 'failed', 'standardOut', 'standardError' // Log out test results to console
                    exceptionFormat 'full'
                }
            }
        }
    }
}



apply from: 'dependencies.gradle'

dependencies {

    implementation supportLibs
    implementation otherLibs
    implementation gson
    // APT dependencies
    annotationProcessor annotationProcessorLibs

    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    testImplementation unitTestLibs
    androidTestImplementation androidTestsLibs

    implementation googlePlayLibs

    implementation rxJavaLibs


    implementation calligraphy

}
repositories {
    mavenCentral()
}